start: _JUNK* (create_phrase _JUNK*)*

create_phrase: create
             | "\"" _JUNK+ create "\""
             | /creates?\b/i WORD ("of" | "that")
             | /creates?\b/i ("a token that's a copy" | "X tokens that are copies") /[^.]+/ "." "\""?
             | "would create"
             | /"(?!create)[^"]+[^,]"/i

create: /creates?\b/i (TOKEN_NAME ",")? definition
create_quoted: "\"" _JUNK+ create? "\""

definition: ("X" | "an") characteristics of_the_chosen? with? named? at_the? where_x_is "."
          | "a number of" (characteristics | PREDEFINED_SUBTYPE /tokens?/) with? equal_to "."
          | quantity "tapped"? (characteristics | PREDEFINED_SUBTYPE /tokens?/) trailing_clause* ". It has" KEYWORD ", \"" RULES_TEXT "\" and" KEYWORD "."
          | quantity "tapped"? (characteristics | PREDEFINED_SUBTYPE /tokens?/) trailing_clause* "." it_has?
          | quantity characteristics named? with_quoted
          | (quantity PREDEFINED_SUBTYPE /tokens?/) ("," "and"? quantity PREDEFINED_SUBTYPE /tokens?/)* "."

trailing_clause: with | at_the | of_the | named | that_are | attached | for_each | "instead" | instead_if | unless_that | that_attacks
quantity: "that many" | WORD
characteristics: LEGENDARY? power_toughness? colors* SUBTYPE* SNOW? TYPE+ /tokens?/
               | LEGENDARY? power_toughness? SUBTYPE+ SNOW? TYPE+ /tokens?/ that_are_colors
where_x_is: ", where X is" (WORD | PUNCTUATION)+
equal_to: "equal to" WORD+

// TOKEN_NAME: WORD (" " WORD)* (", " WORD (" " WORD))?
TOKEN_NAME: /[A-Z][a-z']+/ ((" " | " of " | " of the ") CAP_WORD)*
LEGENDARY: "legendary"
power_toughness: POWER "/" TOUGHNESS
POWER: INT | "*" | "X"
TOUGHNESS: INT | "*" | "X"
colors: COLOR | COLOR "and" COLOR | COLOR "," COLOR ", and" COLOR
COLOR: "white" | "blue" | "black" | "red" | "green" | "colorless"
SNOW: "snow"
SUBTYPE: CAP_WORD
PREDEFINED_SUBTYPE: "Food" | "Clue" | "Treasure" | "Shard" | "Gold" | "Walker"
TYPE: "creature" | "artifact" | "enchantment" | "land" | "planeswalker"
that_are_colors: ("that are" | "that's") COLOR "," COLOR ", and" COLOR // Hazezon Tamar; Godsire

KEYWORD: "protection from black" | "defender" | "flying" | "haste" | "trample" | "reach" | "first strike" | "lifelink" | "deathtouch" | "menace" | ("equip {" INT "}") | "indestructible"
with: "with" KEYWORD ("and" KEYWORD)?
with_quoted: "with" (KEYWORD "and")? "\"" RULES_TEXT "\""
that_are: ("that's" | "that are") ("tapped and"? "attacking" | "blocking that creature" | "blocking target creature attacking you")
that_attacks: "that attacks that player each combat if able"
of_the: "of the chosen color and type"
named: "named" TOKEN_NAME
attached: "attached to" WORD+
for_each: "for each" (WORD | PUNCTUATION)+
at_the: "at the beginning of" ("your" | "the") "next" ("upkeep" | "end step")
instead_if: "instead if" (WORD | PUNCTUATION)+
unless_that: "unless that" (WORD | PUNCTUATION | SYMBOL)+
of_the_chosen: "of the chosen color and type"


it_has: ("It has" | "They have" | "They each have" | "Those creatures have") (KEYWORD "and")? "\"" RULES_TEXT "\""
      | "It has" KEYWORD ", \"" RULES_TEXT "\" and" KEYWORD "."

RULES_TEXT: /[^"]+/


SYMBOL: /\{(?:[WUBRGCXSTQE]|\d+|CHAOS)(?:\/[WUBRGP])?\}/
LOYALTY_COST: /\[(?:\+|−)?[X\d]+\]:/
PUNCTUATION: /[()—',;!•+\-:\/]/
CAP_WORD: /[A-Z][a-z]+/
REMINDER: /\([^)]+\)/
_JUNK: /(?!creates?\b)/i _POSSIBLE_JUNK
_POSSIBLE_JUNK: "." | REMINDER | SYMBOL | LOYALTY_COST | PUNCTUATION | INT | WORD
// _JUNK: "." | SYMBOL | LOYALTY_COST | PUNCTUATION | INT | WORD

WORD: /\w+/
%import common.INT
%import common.WS
%ignore WS